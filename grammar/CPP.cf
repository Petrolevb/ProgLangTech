-- LABEL . VALUE_CAT ::= PROD ;

-- terminator is a token that appear after every item of a list



entrypoints Program ;

Prg . Program ::=  [Expression] ;

DefExprExp . Expression ::= Exp ";" ;
DefExprFunc. Expression ::= Func    ;
terminator Expression "" ;


DefFunc    . Func ::= Signature Arguments Body ;
InlineFunc . Func ::= "inline" Signature Arguments Body ;


DefSig . Signature ::= Type Ident; 


-- Arguments is a list of Argument, or list of signature 
DefArguments . Arguments ::= "(" [Signature] ")" ;
separator Signature "," ;


-- Body is a list of statements
BaseBody   . Body ::= "{" [Statement] "}" ;
terminator Statement "" ;

EmptStm     . Statement ::= ";"                                          ;
ExpStm      . Statement ::= Exp ";"                                      ;
BodyStm     . Statement ::= "{" [Statement] "}"                          ;
ReturnStm   . Statement ::= "return" Exp ";"                             ;
WhileStm    . Statement ::= "while" "(" Exp ")" Body                     ;
DoWhileStm  . Statement ::= "do" Body "while" "(" Exp ")"  ";"           ;
ForStm      . Statement ::= "for" "(" Exp ";" Exp ";" Exp ")" Body       ;
IfSimpleStm . Statement ::= "if" "(" Exp ")" Body                        ;
IfElseStm   . Statement ::= "if" "(" Exp ")" Body "else" Body            ;
SwitchStm   . Statement ::= "switch" "(" Exp ")" "{" [CaseStatement] "}" ;
terminator CaseStatement ";" ;
BaseCaseStm    . CaseStatement ::= "case" Constant ":" Body "break" ";" ;
DefaultCaseStm . CaseStatement ::= "default"       ":" Body "break" ";" ;


SimpleDec . Variables ::= Type Ident ;
MultiDec  . Variables ::= Type Ident "," [Ident] ;
separator nonempty Ident "," ;
Assign    . Variables ::= Type Ident "=" Exp ;

TVoid.  Type ::= "void" ; 
TBool.  Type ::= "bool" ;
TInt.   Type ::= "int" ; 
TDouble.Type ::= "double" ;
TString.Type ::= "string" ;


CInt    . Constant ::= Integer ;
CDouble . Constant ::= Double ;
CFalse  . Constant ::= "false" ;
CTrue   . Constant ::= "true" ;
CString . Constant ::= String ;

EConst  . Exp16 ::= Constant ;
EId     . Exp16 ::= Ident ;

EIndex  . Exp15 ::= Exp15 "[" Exp16 "]" ;
EQConst . Exp15 ::= Exp15 "::" Exp15 ;
ETemId  . Exp15 ::= Exp15 "<" Constant ">" ;
DefFun  . Exp15 ::= Func ;

EIncr   . Exp14 ::= Exp15 "++" ;
EDecr   . Exp14 ::= Exp15 "--" ;
EPoIncr . Exp13 ::= "++" Exp14 ;
EPoDecr . Exp13 ::= "--" Exp14 ;

ENeg    . Exp13 ::= "-" Exp14 ;

EMul  . Exp12 ::= Exp12 "*" Exp13 ; 
EDiv  . Exp12 ::= Exp12 "/" Exp13 ;
EMod  . Exp12 ::= Exp12 "%" Exp13 ;

EAdd  . Exp11 ::= Exp11 "+" Exp12 ;
ESub  . Exp11 ::= Exp11 "-" Exp12 ;

ELShift . Exp10 ::= Exp10 "<<" Exp11 ;
ERShift . Exp10 ::= Exp10 ">>" Exp11 ;

EStSup. Exp9 ::= Exp9 ">" Exp10 ;
EStInf. Exp9 ::= Exp9 "<" Exp10 ;
ESuEg . Exp9 ::= Exp9 ">=" Exp10 ;
EInfEg. Exp9 ::= Exp9 "<=" Exp10 ;

EEq   . Exp8 ::= Exp8 "==" Exp9 ;
EIneq . Exp8 ::= Exp8 "!=" Exp9 ;

EConj . Exp4 ::= Exp4 "&&" Exp5 ;
EDisj . Exp3 ::= Exp3 "||" Exp4 ;
EAss  . Exp2 ::= Exp3 "=" Exp2 ; 

coercions Exp 16 ;
separator Exp "," ;
-- terminator Exp ";" ;

-- position token Identifier (letter (letter | digit | '_')*) ;

comment "/*" "*/" ;
comment "//" ;
comment "#" ;
