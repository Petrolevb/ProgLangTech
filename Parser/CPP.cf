entrypoints Program ;

Prg . Program ::=  [Expression] ;
terminator Expression "" ;

DefUsExp   . Expression ::= "using" [QualConst] ";" ;
TypedefExp . Expression ::= "typedef" [QualConst] CppIdent ";" ;
StructExp  . Expression ::= "struct" CppIdent "{" [StructDecl] "}" ";"      ;
DeclExp    . Expression ::= DeclStatement ";" ;
DefExprFunc. Expression ::= Func    ;

DefFunc    . Func ::=          [QualConst] CppIdent "(" [Arguments] ")" Body ;
InlineFunc . Func ::= "inline" [QualConst] CppIdent "(" [Arguments] ")" Body ;


EmptyBody . Body ::= ";" ;
BodyFun   . Body ::= "{" [Statement] "}" ;

TypeArguments   . Arguments ::= [QualConst] ;
DefArguments    . Arguments ::= [QualConst] CppIdent ;
AffectArguments . Arguments ::= [QualConst] CppIdent "=" Exp;
separator Arguments "," ;


--EmptStm     . Statement ::= ";"                                          ;
ExpStm      . Statement ::= Exp ";"                                      ;
BodyStm     . Statement ::= "{" [Statement] "}"                          ;
DeclStm     . Statement ::= DeclStatement ";"                            ;
TypedefStm  . Statement ::= "typedef" [QualConst] CppIdent  ";"             ;
StructStm   . Statement ::= "struct" CppIdent "{" [StructDecl] "}" ";"      ;
ReturnStm   . Statement ::= "return" Exp ";"                             ;
WhileStm    . Statement ::= "while" "(" Exp ")" Statement                ;
DoWhileStm  . Statement ::= "do" Statement "while" "(" Exp ")"  ";"      ;
ForStm      . Statement ::= "for" "(" DeclStatement ";" Exp ";" Exp ")" Statement  ;
IfSimpleStm . Statement ::= "if" "(" Exp ")" Statement                   ;
IfElseStm   . Statement ::= "if" "(" Exp ")" Statement "else" Statement  ;
SwitchStm   . Statement ::= "switch" "(" Exp ")" "{" [CaseStatement] "}" ;
terminator Statement "" ;
terminator CaseStatement ";" ;
BaseCaseStm    . CaseStatement ::= "case" Constant ":" Statement "break"  ;
DefaultCaseStm . CaseStatement ::= "default"       ":" Statement "break"  ;

StructElem . StructDecl ::= [QualConst] CppIdent ;
terminator StructDecl ";" ;

MultDecl   . DeclStatement ::= [QualConst] [CppIdent]        ;
AffectDecl . DeclStatement ::= [QualConst] CppIdent "=" Exp  ;
separator nonempty CppIdent "," ;


--RefType   . Type ::= Type "&";
--PointType . Type ::= Type "*";
--EmptyArTy . Type ::= Type "[]";
--TemplateTy. Type ::= Type "<" [Type] ">" ;
--separator nonempty Type "," ;
ConstType . Type ::= "const" CppIdentType ;
BaseType  . Type ::=         CppIdentType ;
TVoid   . CppIdentType ::=  "void"   ; 
TBool   . CppIdentType ::=  "bool"   ;
TInt    . CppIdentType ::=  "int"    ;
TDouble . CppIdentType ::=  "double" ;
TPerso  . CppIdentType ::= CppIdent ;

CInt    . Constant ::= [Integer] ;
terminator nonempty Integer ""  ;
CDouble . Constant ::= Double   ;
CFalse  . Constant ::= "false"  ;
CTrue   . Constant ::= "true"   ;
CString . Constant ::= [String] ;
CChar   . Constant ::= Char     ;
terminator nonempty String ""   ;

--Const   . QualConst ::= "const" QualConst ;
Array   . QualConst ::= QualConst "[]" ;
QCPerso . QualConst ::= Type ;
Template. QualConst ::= Type "<" [[QualConst]] ">" ;
Point   . QualConst ::= QualConst "*" ;
Ref     . QualConst ::= QualConst "&" ;
separator nonempty [QualConst] "," ;
separator nonempty QualConst "::" ;

EConst  . Exp16 ::= Constant;

EQualConst . Exp15 ::= [QualConst] ;
EIndex     . Exp15 ::= Exp15 "[" Exp "]" ;
FunCall    . Exp15 ::= Exp15 "(" [Exp] ")" ;

EPointProj . Exp14 ::= Exp14 "." Exp15  ;
EArrowProj . Exp14 ::= Exp14 "->" Exp15 ;

EIncr   . Exp14 ::= Exp15 "++" ;
EDecr   . Exp14 ::= Exp15 "--" ;
EDeferen. Exp14 ::= "*" Exp15 ;

EPoIncr . Exp13 ::= "++" Exp14 ;
EPoDecr . Exp13 ::= "--" Exp14 ;
ENeg    . Exp13 ::= "!" Exp14 ;

EMul  . Exp12 ::= Exp12 "*" Exp13 ; 
EDiv  . Exp12 ::= Exp12 "/" Exp13 ;
EMod  . Exp12 ::= Exp12 "%" Exp13 ;

EAdd  . Exp11 ::= Exp11 "+" Exp12 ;
ESub  . Exp11 ::= Exp11 "-" Exp12 ;

ELShift . Exp10 ::= Exp10 "<<" Exp11 ;

ERShift . Exp10 ::= Exp10 ">>" Exp11 ;

EStInf. Exp9 ::= Exp9 "<" Exp10 ;
EStSup. Exp9 ::= Exp9 ">" Exp10 ;
ESuEg . Exp9 ::= Exp9 ">=" Exp10 ;
EInfEg. Exp9 ::= Exp9 "<=" Exp10 ;

EEq   . Exp8 ::= Exp8 "==" Exp9 ;
EIneq . Exp8 ::= Exp8 "!=" Exp9 ;

EConj . Exp4 ::= Exp4 "&&" Exp5 ;

EDisj . Exp3 ::= Exp3 "||" Exp4 ;

EAss  . Exp2 ::= Exp14 "=" Exp2  ; 

EPlAss. Exp2 ::= Exp14 "+=" Exp2 ;
ESuAss. Exp2 ::= Exp14 "-=" Exp2 ;

EConditional . Exp2 ::= Exp3 "?" Exp ":" Exp3 ;

Exception    . Exp1 ::= "throw" Exp2 ;

coercions Exp 16 ;
separator Exp "," ;

position token CppIdent (letter (letter | digit | '_')* );


comment "/*" "*/" ;
comment "//" ;
comment "#" ;
