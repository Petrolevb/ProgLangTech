entrypoints Program ;

Prg . Program ::=  [Expression] ;
terminator Expression "" ;

DefExprFunc. Expression ::= Func    ;
DefUsExp   . Expression ::= "using" [QualConst] ";" ;
TypedefExp . Expression ::= "typedef" [QualConst] Ident ";" ;
DeclExp    . Expression ::= Arguments ";" ;

DefFunc    . Func ::=          Type Ident "(" [Arguments] ")" Body ;
InlineFunc . Func ::= "inline" Type Ident "(" [Arguments] ")" Body ;


EmptyBody . Body ::= ";" ;
BodyFun   . Body ::= "{" [Statement] "}" ;

TypeArguments   . Arguments ::= Type ;
DefArguments    . Arguments ::= Type Ident ;
AffectArguments . Arguments ::= Type Ident "=" Constant;
separator Arguments "," ;


EmptStm     . Statement ::= ";"                                          ;
ExpStm      . Statement ::= Exp ";"                                      ;
BodyStm     . Statement ::= "{" [Statement] "}"                          ;
TypedefStm  . Statement ::= "typedef" [QualConst] Ident  ";"                      ;
ReturnStm   . Statement ::= "return" Exp ";"                             ;
WhileStm    . Statement ::= "while" "(" Exp ")" Statement                ;
DoWhileStm  . Statement ::= "do" Statement "while" "(" Exp ")"  ";"      ;
ForStm      . Statement ::= "for" "(" Exp ";" Exp ";" Exp ")" Statement  ;
IfSimpleStm . Statement ::= "if" "(" Exp ")" Statement                   ;
IfElseStm   . Statement ::= "if" "(" Exp ")" Statement "else" Statement  ;
SwitchStm   . Statement ::= "switch" "(" Exp ")" "{" [CaseStatement] "}" ;
terminator Statement "" ;
terminator CaseStatement ";" ;
BaseCaseStm    . CaseStatement ::= "case" Constant ":" Statement "break"  ;
DefaultCaseStm . CaseStatement ::= "default"       ":" Statement "break"  ;



ConstType . Type ::= "const" Type;
RefType   . Type ::= Type "&";
PointType . Type ::= Type "*";
EmptyArTy . Type ::= Type "[]";
TemplateTy. Type ::= Type "<" [Type] ">" ;
separator nonempty Type "," ;
BaseType  . Type ::= IdentType ;
TVoid   . IdentType ::=  "void"   ; 
TBool   . IdentType ::=  "bool"   ;
TInt    . IdentType ::=  "int"    ;
TDouble . IdentType ::=  "double" ;
TPerso  . IdentType ::= Ident ;

CInt    . Constant ::= [Integer] ;
terminator nonempty Integer ""  ;
CDouble . Constant ::= Double   ;
CFalse  . Constant ::= "false"  ;
CTrue   . Constant ::= "true"   ;
CString . Constant ::= [String] ;
CChar   . Constant ::= Char     ;
terminator nonempty String ""   ;

QCPerso . QualConst ::= Type ;
--Template. QualConst ::= Type "<" Type ">" ;

EConst  . Exp16 ::= Constant;

EQualConst . Exp15 ::= [QualConst] ;
separator nonempty QualConst "::" ;
EIndex     . Exp15 ::= Exp15 "[" Exp "]" ;
FunCall    . Exp15 ::= Exp15 "(" [Exp] ")" ;
VarDeclExp . Exp15 ::= Exp15 [Ident] ;
separator nonempty Ident "," ;

EPointProj . Exp14 ::= Exp14 "." Exp15  ;
EArrowProj . Exp14 ::= Exp14 "->" Exp15 ;

EIncr   . Exp14 ::= Exp15 "++" ;
EDecr   . Exp14 ::= Exp15 "--" ;
EDeferen. Exp14 ::= "*" Exp15 ;

EPoIncr . Exp13 ::= "++" Exp14 ;
EPoDecr . Exp13 ::= "--" Exp14 ;
ENeg    . Exp13 ::= "!" Exp14 ;

EMul  . Exp12 ::= Exp12 "*" Exp13 ; 
EDiv  . Exp12 ::= Exp12 "/" Exp13 ;
EMod  . Exp12 ::= Exp12 "%" Exp13 ;

EAdd  . Exp11 ::= Exp11 "+" Exp12 ;
ESub  . Exp11 ::= Exp11 "-" Exp12 ;

ELShift . Exp10 ::= Exp10 "<<" Exp11 ;

ERShift . Exp10 ::= Exp10 ">>" Exp11 ;

EStInf. Exp9 ::= Exp9 "<" Exp10 ;
EStSup. Exp9 ::= Exp9 ">" Exp10 ;
ESuEg . Exp9 ::= Exp9 ">=" Exp10 ;
EInfEg. Exp9 ::= Exp9 "<=" Exp10 ;

EEq   . Exp8 ::= Exp8 "==" Exp9 ;
EIneq . Exp8 ::= Exp8 "!=" Exp9 ;

EConj . Exp4 ::= Exp4 "&&" Exp5 ;

EDisj . Exp3 ::= Exp3 "||" Exp4 ;

EAss  . Exp2 ::= Exp14 "=" Exp2  ; 

EPlAss. Exp2 ::= Exp14 "+=" Exp2 ;
ESuAss. Exp2 ::= Exp14 "-=" Exp2 ;

EConditional . Exp2 ::= Exp3 "?" Exp ":" Exp3 ;

Exception    . Exp1 ::= "throw" Exp2 ;

coercions Exp 16 ;
separator Exp "," ;

comment "/*" "*/" ;
comment "//" ;
comment "#" ;
